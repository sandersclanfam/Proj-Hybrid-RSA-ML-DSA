add_subdirectory(aes)
add_subdirectory(container)
add_subdirectory(asymmetric_container)
add_subdirectory(key_exchange)
add_subdirectory(sha3)
add_subdirectory(prng)
add_subdirectory(ssd_hdd_removal)
add_subdirectory(signature)
add_subdirectory(pbkdf2)

function(add_tests_for_examples PATH)
    get_property(TARGETS DIRECTORY "${PATH}" PROPERTY BUILDSYSTEM_TARGETS)
    foreach(TARGET IN LISTS TARGETS)
        get_property(BINARY_DIR TARGET "${TARGET}" PROPERTY BINARY_DIR)
        string(TOUPPER "${TARGET}" "TARGET_UPPER")
        add_test(NAME "EXAMPLE.${TARGET_UPPER}" COMMAND "$<TARGET_FILE:${TARGET}>" WORKING_DIRECTORY "${BINARY_DIR}")
    endforeach()

    get_property(SUBDIRECTORIES DIRECTORY "${PATH}" PROPERTY SUBDIRECTORIES)
    foreach(SUBPATH IN LISTS SUBDIRECTORIES)
        add_tests_for_examples("${SUBPATH}")
    endforeach()
endfunction()

add_tests_for_examples("${CMAKE_CURRENT_SOURCE_DIR}")
